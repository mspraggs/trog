class Error {
    #[constructor]
    fn new(self, context) {
        self.context = context;
    }
}

#[derive(Error)]
class RuntimeError {}

#[derive(Error)]
class AttributeError {}

#[derive(Error)]
class IndexError {}

#[derive(Error)]
class ImportError {}

#[derive(Error)]
class NameError {}

#[derive(Error)]
class TypeError {}

#[derive(Error)]
class ValueError {}

#[derive(Error)]
class StopIter {
    #[constructor]
    fn new(self) {
        super.new(nil);
    }
}

class Iter {
    fn iter(self) {
        return self;
    }

    fn map(self, f) {
        return MapIter.new(self.iter(), f);
    }

    fn collect(self) {
        var ret = [];
        for v in self {
            ret.push(v);
        }
        return ret;
    }

    fn filter(self, pred) {
        return FilterIter.new(self.iter(), pred);
    }

    fn reduce(self, func, init) {
        var ret = init;
        for v in self {
            ret = func(ret, v);
        }
        return ret;
    }
}

#[derive(Iter)]
class MapIter {
    #[constructor]
    fn new(self, iterable, func) {
        self.iterable = iterable;
        self.func = func;
    }

    fn iter(self) {
        return self;
    }

    fn next(self) {
        var next = self.iterable.next();
        if next.derives(StopIter) {
            return next;
        }
        return self.func(next);
    }
}

#[derive(Iter)]
class FilterIter {
    #[constructor]
    fn new(self, iterable, predicate) {
        self.iterable = iterable;
        self.predicate = predicate;
    }

    fn iter(self) {
        return self;
    }

    fn next(self) {
        var next = self.iterable.next();
        while !next.derives(StopIter) && !self.predicate(next) {
            next = self.iterable.next();
        }
        return next;
    }
}
