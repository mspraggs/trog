class Iter {
    fn map(self, f) {
        return MapIter(self.__iter__(), f);
    }

    fn collect(self) {
        var ret = [];
        for v in self {
            ret.push(v);
        }
        return ret;
    }

    fn filter(self, pred) {
        return FilterIter(self.__iter__(), pred);
    }

    fn reduce(self, func, init) {
        var ret = init;
        for v in self {
            ret = func(ret, v);
        }
        return ret;
    }
}

class MapIter < Iter {
    fn __init__(self, iterable, func) {
        self.iterable = iterable;
        self.func = func;
        self.sentinel = sentinel();
    }

    fn __iter__(self) {
        return self;
    }

    fn __next__(self) {
        var next = self.iterable.__next__();
        if next == self.sentinel {
            return self.sentinel;
        }
        return self.func(next);
    }
}

class FilterIter < Iter {
    fn __init__(self, iterable, predicate) {
        self.iterable = iterable;
        self.predicate = predicate;
        self.sentinel = sentinel();
    }

    fn __iter__(self) {
        return self;
    }

    fn __next__(self) {
        var sentinel = self.sentinel;
        var next = self.iterable.__next__();
        while next != sentinel and !self.predicate(next) {
            next = self.iterable.__next__();
        }
        return next;
    }
}
