class Iter {
    fn iter(self) {
        return self;
    }

    fn map(self, f) {
        return MapIter(self.iter(), f);
    }

    fn collect(self) {
        var ret = [];
        for v in self {
            ret.push(v);
        }
        return ret;
    }

    fn filter(self, pred) {
        return FilterIter(self.iter(), pred);
    }

    fn reduce(self, func, init) {
        var ret = init;
        for v in self {
            ret = func(ret, v);
        }
        return ret;
    }
}

class MapIter < Iter {
    fn __init__(self, iterable, func) {
        self.iterable = iterable;
        self.func = func;
        self.sentinel = sentinel();
    }

    fn iter(self) {
        return self;
    }

    fn next(self) {
        var next = self.iterable.next();
        if next == self.sentinel {
            return self.sentinel;
        }
        return self.func(next);
    }
}

class FilterIter < Iter {
    fn __init__(self, iterable, predicate) {
        self.iterable = iterable;
        self.predicate = predicate;
        self.sentinel = sentinel();
    }

    fn iter(self) {
        return self;
    }

    fn next(self) {
        var sentinel = self.sentinel;
        var next = self.iterable.next();
        while next != sentinel and !self.predicate(next) {
            next = self.iterable.next();
        }
        return next;
    }
}
